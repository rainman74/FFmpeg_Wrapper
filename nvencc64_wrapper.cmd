@echo off & setlocal enabledelayedexpansion

:INIT
call setesc
if '%1'=='-h' goto USAGE
if '%1'=='' goto USAGE

set "EDIT_TAGS=1"
set "DEBUG_AUTOCROP=0"
if "%DEBUG_AUTOCROP%"=="1" (
	set "DBG=call :DEBUG"
) else (
	set "DBG=call :NOP"
)

call :VALIDATE-PARAMS %*
if "!PARAM_ERR!"=="1" goto :END
call :SETENCODER %1 %2 %3 %4 %5 %6 %7
call :SETAUDIO	 %1 %2 %3 %4 %5 %6 %7
call :SETCROP	 %1 %2 %3 %4 %5 %6 %7
call :SETFILTER	 %1 %2 %3 %4 %5 %6 %7
set "FILTER_HAS_RESIZE=0"
if defined FILTER (
	echo(!FILTER! | findstr /i "--vpp-resize" >nul && set "FILTER_HAS_RESIZE=1"
)
call :SETMODE	 %1 %2 %3 %4 %5 %6 %7
if defined MODE (
	echo(!MODE! | findstr /i "--vpp-resize" >nul && set "FILTER_HAS_RESIZE=1"
)
call :SETDECODER %1 %2 %3 %4 %5 %6 %7

set "REQ_Q=%3"
if "!REQ_Q!"=="" set "REQ_Q=def"

call :MAIN
goto :END

:MAIN
call :ENSURE_DIR "_Converted"
for %%I in (*.mkv *.mp4 *.mpg *.mov *.avi *.webm) do if not exist "_Converted\%%~nI.mkv" (
	set "FILENAME=%%~nI"
	set "SKIP_FILE="
	set "RESIZE_PARAM="
	set "CROP_L=0"
	set "CROP_R=0"
	set "TARGET_DIR="
	set "RESIZE_REQUIRED=0"
	set "SRC_CODEC="

	for /f "usebackq delims=" %%C in ('mediainfo "--Inform=Video;%%Format%% %%CodecID%%" "%%I"') do set "SRC_CODEC=%%C"
	echo !SRC_CODEC! | findstr /i /r /c:"HEVC" /c:"H.265" /c:"V_MPEGH/ISO/HEVC" >nul
	if not errorlevel 1 (
		if /i "%ENCODER%"=="hevc" set "TARGET_DIR=_Converted"
		if /i "%ENCODER%"=="he10" set "TARGET_DIR=_Converted"
	)
	echo !SRC_CODEC! | findstr /i /r /c:"AVC" /c:"H.264" /c:"V_MPEG4/ISO/AVC" >nul
	if not errorlevel 1 (
		if /i "%ENCODER%"=="h264" set "TARGET_DIR=_Converted"
	)
	echo !SRC_CODEC! | findstr /i /r /c:"AV1" /c:"V_AV1" >nul
	if not errorlevel 1 (
		if /i "%ENCODER%"=="av1" set "TARGET_DIR=_Converted"
	)
	if defined TARGET_DIR (
		call :ENSURE_DIR "!TARGET_DIR!"
		echo %ESC%[91mWARNING: Source already encoded as !SRC_CODEC!. Moving file to !TARGET_DIR!.%ESC%[0m
		move "%%I" "!TARGET_DIR!\" >nul
		set "SKIP_FILE=1"
		if "%EDIT_TAGS%"=="1" call :EDIT_TAGS "!TARGET_DIR!\%%I"
	)

	if not defined SKIP_FILE (
		%DBG% ==========================================
		%DBG% File: %%I
		%DBG% CROP_MODE: "!CROP_MODE!"
		%DBG% ==========================================

		if "%ENCODER%"=="h264" (
			call :SETQUALITY-H264
		) else (
			call :SETQUALITY-HEVC
		)
		echo %ESC%[101;93m %%I %ESC%[0m
		if "!REQ_Q!"=="auto" (
			echo "!FILENAME!" | findstr /c:"(19" >nul || echo "!FILENAME!" | findstr /c:"(20" >nul || (
				echo %ESC%[91mWARNING: No year found in filename. Falling back to default quality ^(!QUALITY!^).%ESC%[0m
			)
		)
		
		if /i "!CROP_MODE!"=="AUTO" (
			set "PROBE_OK=0"
			%DBG% RUN_PROBE is being executed
			call :RUN_PROBE "%%I"
			if "!PROBE_OK!"=="0" (
				%DBG% RUN_PROBE failed, moving file to _Check
				call :ENSURE_DIR "_Check"
				move "%%I" "_Check\" >nul
				set "SKIP_FILE=1"
			) else (
				if "!AUTO_CROP!"=="0:0:0:0" (
					%DBG% AUTO-CROP: no crop detected, passthrough
					set "RESIZE_REQUIRED=0"
					set "CROP="
					set "RESIZE_PARAM="
				) else (
					set "AUTO_CROP_FIX=!AUTO_CROP::=,!"
					set "CROP=--crop !AUTO_CROP_FIX! --output-res !AUTO_RES!"
					for /f "tokens=1,3 delims=:" %%A in ("!AUTO_CROP!") do (
						set "CROP_L=%%A"
						set "CROP_R=%%C"
					)	
				)
				%DBG% AUTO-CROP final result: !CROP!
			)
		)

		if not "!CROP_MODE!"=="AUTO" if defined CROP (
			echo !CROP! | findstr /i "--output-res" >nul && (
				if not "!CROP!"=="--crop 0,0,0,0" (
					set "RESIZE_REQUIRED=1"
				)
			)
		)

		setlocal DisableDelayedExpansion
		echo "file:\\\%%~dI%%~pI"| sed -r "s/[\"]/\a/g; s/[\\]/\//g; s/[ ]/\%%20/g; s/[#]/\%%23/g; s/[']/\%%27/g; s/!/%%21/g"
		setlocal EnableDelayedExpansion

		mediainfo --Inform="General;%%Duration/String2%% - %%FileSize/String4%%" "%%I"

		%DBG% NVEnc parameters:
		%DBG%   CROP   = "!CROP!"
		%DBG%   FILTER = "!FILTER!"
		%DBG%   MODE   = "!MODE!"
		%DBG%   AUDIO  = "!AUDIO!"

		if not defined SKIP_FILE (
			set "RESIZE_PARAM="
			if "!RESIZE_REQUIRED!"=="1" if "!FILTER_HAS_RESIZE!"=="0" (
				set "RESIZE_PARAM=--vpp-resize spline36"
			)

			%DBG% RESIZE_REQUIRED   = "!RESIZE_REQUIRED!"
			%DBG% FILTER_HAS_RESIZE = "!FILTER_HAS_RESIZE!"
			%DBG% RESIZE_PARAM      = "!RESIZE_PARAM!"

			nvencc64.exe --thread-priority all=lowest --input-thread 1 --output-buf 16 --%DECODER% -i "%%I" -c %ENCODER% --profile %PROFILE% --tier high --level auto --qvbr !QUALITY! !PRESET! --aq-temporal --aq-strength 0 !TUNING! --bref-mode middle !RESIZE_PARAM! !CROP! !FILTER! !MODE! !AUDIO! --sub-copy --chapter-copy -o "_Converted\%%~nI.mkv"

			if exist "_Converted\%%~nI.mkv" (
				if "%EDIT_TAGS%"=="1" call :EDIT_TAGS "_Converted\%%~nI.mkv"
			)

			for /L %%X in (5,-1,1) do (echo Waiting for %%X seconds... & sleep 1s)
			echo.
		)
	)
)
exit /b

:SETQUALITY-HEVC
set "ACTUAL_Q=!REQ_Q!"
if "!REQ_Q!"=="auto" (
	set "ACTUAL_Q=none"
	echo "!FILENAME!" | findstr /c:"(19" >nul && set "ACTUAL_Q=hq"
	echo "!FILENAME!" | findstr /c:"(20" >nul && set "ACTUAL_Q=def"
	if "!ACTUAL_Q!"=="none" set "ACTUAL_Q=def"
)
set "PRESET=--preset quality"
set "TUNING=--tune hq"
if "!ACTUAL_Q!"=="uhq" 		(set "QUALITY=24" & set "TUNING=--tune uhq")
if "!ACTUAL_Q!"=="hq"  		(set "QUALITY=26")
if "!ACTUAL_Q!"=="def" 		(set "QUALITY=28")
if "!ACTUAL_Q!"=="lq"  		(set "QUALITY=30")
if "!ACTUAL_Q!"=="ulq" 		(set "QUALITY=32" & set "TUNING=--tune undef" & set "PRESET=--preset performance")
exit /b

:SETQUALITY-H264
set "ACTUAL_Q=!REQ_Q!"
if "!REQ_Q!"=="auto" (
	set "ACTUAL_Q=none"
	echo "!FILENAME!" | findstr /c:"(19" >nul && set "ACTUAL_Q=hq"
	echo "!FILENAME!" | findstr /c:"(20" >nul && set "ACTUAL_Q=def"
	if "!ACTUAL_Q!"=="none" set "ACTUAL_Q=def"
)
set "PRESET=--preset quality" & set "TUNING=--tune hq"
if "!ACTUAL_Q!"=="uhq" 		(set "QUALITY=20" & set "TUNING=--tune uhq")
if "!ACTUAL_Q!"=="hq"  		(set "QUALITY=22")
if "!ACTUAL_Q!"=="def" 		(set "QUALITY=24")
if "!ACTUAL_Q!"=="lq"  		(set "QUALITY=26")
if "!ACTUAL_Q!"=="ulq" 		(set "QUALITY=28" & set "TUNING=--tune undef" & set "PRESET=--preset performance")
exit /b

:SETENCODER
set "ENCODER=hevc" & set "PROFILE=main"
if "%1"=="def"				(set "ENCODER=hevc" & set "PROFILE=main")
if "%1"=="hevc" 			(set "ENCODER=hevc" & set "PROFILE=main")
if "%1"=="he10" 			(set "ENCODER=hevc" & set "PROFILE=main10 --output-depth 10")
if "%1"=="h264" 			(set "ENCODER=h264" & set "PROFILE=high")
if "%1"=="av1"				(set "ENCODER=av1"  & set "PROFILE=high")
exit /b

:SETAUDIO
set "AUDIO=--audio-codec ac3 --audio-bitrate stereo:192,5.1:384 --audio-encode-other-codec-only"
if "%2"=="copy"	  			(set "AUDIO=--audio-copy")
if "%2"=="copy1"  			(set "AUDIO=--audio-copy 1")
if "%2"=="copy2"  			(set "AUDIO=--audio-copy 2")
if "%2"=="copy12" 			(set "AUDIO=--audio-copy 1,2")
if "%2"=="copy23" 			(set "AUDIO=--audio-copy 2,3")
if "%2"=="ac3"	  			(set "AUDIO=--audio-codec ac3 --audio-bitrate stereo:192,5.1:384 --audio-encode-other-codec-only")
if "%2"=="aac"	  			(set "AUDIO=--audio-codec aac --audio-bitrate stereo:128,5.1:256 --audio-encode-other-codec-only")
if "%2"=="eac3"	  			(set "AUDIO=--audio-codec eac3 --audio-bitrate stereo:320,5.1:640 --audio-encode-other-codec-only")
exit /b

:SETCROP
set "CROP=" & set "CROP_MODE="
if "%4"=="auto" (
	set "CROP_MODE=AUTO"
	exit /b
)
if "%4"=="copy"				(set "CROP=")
if "%4"=="696"				(set "CROP=--crop 0,192,0,192")
if "%4"=="768"				(set "CROP=--crop 0,156,0,156")
if "%4"=="800"				(set "CROP=--crop 0,140,0,140")
if "%4"=="804"				(set "CROP=--crop 0,138,0,138")
if "%4"=="808"				(set "CROP=--crop 0,136,0,136")
if "%4"=="812"				(set "CROP=--crop 0,134,0,134")
if "%4"=="816"				(set "CROP=--crop 0,132,0,132")
if "%4"=="872"				(set "CROP=--crop 0,104,0,104")
if "%4"=="960"				(set "CROP=--crop 0,60,0,60")
if "%4"=="1012"				(set "CROP=--crop 0,34,0,34")
if "%4"=="1024"				(set "CROP=--crop 0,28,0,28")
if "%4"=="1036"				(set "CROP=--crop 0,22,0,22")
if "%4"=="1036p"			(set "CROP=--output-res 1920x1036 --crop 0,0,0,0")
if "%4"=="1040"				(set "CROP=--crop 0,20,0,20")
if "%4"=="720"				(set "CROP=--output-res 1280x-2 --crop 0,0,0,0")
if "%4"=="720p"				(set "CROP=--output-res -2x720 --crop 0,0,0,0")
if "%4"=="720f"				(set "CROP=--output-res 1280x720 --crop 0,0,0,0")
if "%4"=="1080"				(set "CROP=--output-res 1920x-2 --crop 0,0,0,0")
if "%4"=="1080p"			(set "CROP=--output-res -2x1080 --crop 0,0,0,0")
if "%4"=="1080f"			(set "CROP=--output-res 1920x1080 --crop 0,0,0,0")
if "%4"=="2160"				(set "CROP=--output-res 3840x-2 --crop 0,0,0,0")
if "%4"=="2160p"			(set "CROP=--output-res -2x2160 --crop 0,0,0,0")
if "%4"=="2160f"			(set "CROP=--output-res 3840x2160 --crop 0,0,0,0")
if "%4"=="1440"				(set "CROP=--output-res 1440x1080 --crop 240,0,240,0")
if "%4"=="1348"				(set "CROP=--output-res 1348x1080 --crop 286,0,286,0")
if "%4"=="1420"				(set "CROP=--output-res 1420x1080 --crop 250,0,250,0")
if "%4"=="1480"				(set "CROP=--output-res 1480x1080 --crop 220,0,220,0")
if "%4"=="1500"				(set "CROP=--output-res 1500x1080 --crop 210,0,210,0")
if "%4"=="1792"				(set "CROP=--output-res 1792x1080 --crop 64,0,64,0")
if "%4"=="1764"				(set "CROP=--output-res 1764x1080 --crop 78,0,78,0")
if "%4"=="1780"				(set "CROP=--output-res 1780x1080 --crop 70,0,70,0")
if "%4"=="1788"				(set "CROP=--output-res 1788x1080 --crop 66,0,66,0")
if "%4"=="1800"				(set "CROP=--output-res 1800x1080 --crop 60,0,60,0")
if "%4"=="c1"				(set "CROP=")
if "%4"=="c2"				(set "CROP=")
if "%4"=="c3"				(set "CROP=")
if "%4"=="c4"				(set "CROP=")
if "%4"=="c5"				(set "CROP=")
if "%4"=="c6"				(set "CROP=")
exit /b

:SETFILTER
set "FILTER="
if "%5"=="copy"			 	(set "FILTER=")
if "%5"=="edgelevel"	 	(set "FILTER=--vpp-edgelevel")
if "%5"=="smooth"		 	(set "FILTER=--vpp-smooth")
if "%5"=="smooth31"		 	(set "FILTER=--vpp-smooth quality=6,qp=31,prec=fp32")
if "%5"=="smooth63"		 	(set "FILTER=--vpp-smooth quality=6,qp=63,prec=fp32")
if "%5"=="nlmeans"		 	(set "FILTER=--vpp-nlmeans")
if "%5"=="gauss"		 	(set "FILTER=--vpp-gauss 3")
if "%5"=="gauss5"		 	(set "FILTER=--vpp-gauss 5")
if "%5"=="sharp"		 	(set "FILTER=--vpp-unsharp")
if "%5"=="ss"			 	(set "FILTER=--vpp-smooth --vpp-unsharp")
if "%5"=="denoise"		 	(set "FILTER=--vpp-nvvfx-denoise strength=0")
if "%5"=="denoisehq"	 	(set "FILTER=--vpp-nvvfx-denoise strength=1")
if "%5"=="artifact"		 	(set "FILTER=--vpp-nvvfx-artifact-reduction mode=0")
if "%5"=="artifacthq"	 	(set "FILTER=--vpp-nvvfx-artifact-reduction mode=1")
if "%5"=="superres"		 	(set "FILTER=--vpp-resize algo=nvvfx-superres,superres-mode=0,superres-strength=0.4")
if "%5"=="superreshq"	 	(set "FILTER=--vpp-resize algo=nvvfx-superres,superres-mode=1,superres-strength=0.4")
if "%5"=="vsr"			 	(set "FILTER=--vpp-resize algo=ngx-vsr,vsr-quality=4 --vpp-unsharp")
if "%5"=="vsrdenoise"	 	(set "FILTER=--vpp-resize algo=ngx-vsr,vsr-quality=4 --vpp-unsharp --vpp-nvvfx-denoise strength=0")
if "%5"=="vsrdenoisehq"	 	(set "FILTER=--vpp-resize algo=ngx-vsr,vsr-quality=4 --vpp-unsharp --vpp-nvvfx-denoise strength=1")
if "%5"=="vsrartifact"	 	(set "FILTER=--vpp-resize algo=ngx-vsr,vsr-quality=4 --vpp-unsharp --vpp-nvvfx-artifact-reduction mode=0")
if "%5"=="vsrartifacthq" 	(set "FILTER=--vpp-resize algo=ngx-vsr,vsr-quality=4 --vpp-unsharp --vpp-nvvfx-artifact-reduction mode=1")
if "%5"=="log"			 	(set "FILTER=--log-packets input_packets.log")
if "%5"=="f1"			 	(set "FILTER=")
if "%5"=="f2"			 	(set "FILTER=")
if "%5"=="f3"			 	(set "FILTER=")
if "%5"=="f4"			 	(set "FILTER=")
if "%5"=="f5"			 	(set "FILTER=")
if "%5"=="f6"			 	(set "FILTER=")
exit /b

:SETMODE
set "MODE="
if "%6"=="copy"				(set "MODE=")
if "%6"=="deint"			(set "MODE=--interlace auto --vpp-deinterlace adaptive")
if "%6"=="yadif"			(set "MODE=--interlace auto --vpp-yadif mode=auto")
if "%6"=="yadifbob"			(set "MODE=--interlace auto --vpp-yadif mode=bob --vpp-select-every 2")
if "%6"=="double"			(set "MODE=--vpp-fruc double")
if "%6"=="23fps"			(set "MODE=--fps 24000/1001")
if "%6"=="25fps"			(set "MODE=--fps 25.0")
if "%6"=="30fps"			(set "MODE=--fps 30.0")
if "%6"=="60fps"			(set "MODE=--fps 60.0")
if "%6"=="29fps"			(set "MODE=--fps 30000/1001")
if "%6"=="59fps"			(set "MODE=--fps 60000/1001")
if "%6"=="tweak"			(set "MODE=--vpp-tweak brightness=0.0,contrast=1.0,gamma=1.0,saturation=1.0,hue=0.0")
if "%6"=="brighter"			(set "MODE=--vpp-curves preset=lighter")
if "%6"=="darker"			(set "MODE=--vpp-curves preset=darker")
if "%6"=="vintage"			(set "MODE=--vpp-curves preset=vintage")
if "%6"=="linear"			(set "MODE=--vpp-curves green=0/0 0.5/0.5 1/1:red=0/0 0.5/0.5 1/1:blue=0/0 0.5/0.5 1/1")
if "%6"=="HDRtoSDR"			(set "MODE=--vpp-colorspace matrix=bt2020nc:bt709,colorprim=bt2020:bt709,transfer=smpte2084:bt709,range=auto:auto,hdr2sdr=bt2390")
if "%6"=="HDRtoSDRR"		(set "MODE=--vpp-colorspace matrix=bt2020nc:bt709,colorprim=bt2020:bt709,transfer=smpte2084:bt709,range=auto:auto,hdr2sdr=reinhard")
if "%6"=="HDRtoSDRM"		(set "MODE=--vpp-colorspace matrix=bt2020nc:bt709,colorprim=bt2020:bt709,transfer=smpte2084:bt709,range=auto:auto,hdr2sdr=mobius")
if "%6"=="HDRtoSDRH"		(set "MODE=--vpp-colorspace matrix=bt2020nc:bt709,colorprim=bt2020:bt709,transfer=smpte2084:bt709,range=auto:auto,hdr2sdr=hable")
if "%6"=="dv"				(set "MODE=--dolby-vision-profile copy --dolby-vision-rpu copy --master-display copy --max-cll copy")
if "%6"=="dolby-vision"		(set "MODE=--dolby-vision-profile copy --dolby-vision-rpu copy --master-display copy --max-cll copy")
exit /b

:SETDECODER
set "DECODER=avhw"
if "%7"=="def"				(set "DECODER=avhw")
if "%7"=="hw"				(set "DECODER=avhw")
if "%7"=="sw"				(set "DECODER=avsw")
if "%7"=="auto" 			(set "DECODER=")
exit /b

:VALIDATE-PARAMS
set "PARAM_ERR=0"
if not "%3"=="" (
	set "VALID=0"
	for %%A in (def auto hq uhq lq ulq) do if /i "%3"=="%%A" set "VALID=1"
	if "!VALID!"=="0" (set "ERR_MSG=Invalid quality "%3" at position 3. Valid values: [def|auto|hq|uhq|lq|ulq]" & goto :PARAM_ERROR)
)
if not "%4"=="" (
	set "VALID=0"
	for %%A in (copy auto c1 c2 c3 c4 c5 c6 696 768 800 804 808 812 816 872 960 1012 1024 1036 1036p 1040 720 720p 720f 1080 1080p 1080f 2160 2160p 2160f 1440 1348 1420 1480 1500 1792 1764 1780 1788 1800) do if /i "%4"=="%%A" set "VALID=1"
	if "!VALID!"=="0" (set "ERR_MSG=Invalid crop/resolution "%4" at position 4." & goto :PARAM_ERROR)
)
if not "%5"=="" (
	set "VALID=0"
	for %%A in (copy f1 f2 f3 f4 f5 f6 edgelevel smooth smooth31 smooth63 nlmeans gauss gauss5 sharp ss denoise denoisehq artifact artifacthq superres superreshq vsr vsrdenoise vsrdenoisehq vsrartifact vsrartifacthq log) do if /i "%5"=="%%A" set "VALID=1"
	if "!VALID!"=="0" (set "ERR_MSG=Invalid filter "%5" at position 5." & goto :PARAM_ERROR)
)
if not "%6"=="" (
	set "VALID=0"
	for %%A in (copy deint yadif yadifbob double 23fps 25fps 30fps 60fps 29fps 59fps brighter darker vintage linear tweak HDRtoSDR HDRtoSDRR HDRtoSDRM HDRtoSDRH dv dolby-vision) do if /i "%6"=="%%A" set "VALID=1"
	if "!VALID!"=="0" (set "ERR_MSG=Invalid mode "%6" at position 6." & goto :PARAM_ERROR)
)
exit /b

:PARAM_ERROR
set "PARAM_ERR=1"
echo %ESC%[91mERROR: !ERR_MSG!%ESC%[0m
echo.
echo Your command was: %0 %*
exit /b

:EDIT_TAGS
if not exist "%~1" exit /b 1
setlocal EnableDelayedExpansion
set "S=" & set "E="
set "FILE=%~1"

set "PS_SCRIPT=%TEMP%\edit_tags_%RANDOM%.ps1"
set "PS_SET_FILE=%TEMP%\edit_tags_set_%RANDOM%.cmd"

if exist "%PS_SCRIPT%" del "%PS_SCRIPT%"
if exist "%PS_SET_FILE%" del "%PS_SET_FILE%"

for /f "tokens=1 delims=:" %%A in ('findstr /n "^#PS_EDIT_TAGS_BEGIN#" "%~f0"') do set /a S=%%A
for /f "tokens=1 delims=:" %%A in ('findstr /n "^#PS_EDIT_TAGS_END#"   "%~f0"') do set /a E=%%A-S

if not defined S exit /b 9
set /a E=E
if %E% LEQ 0 exit /b 9

more +%S% "%~f0" | head -n %E% > "%PS_SCRIPT%"

powershell.exe -NoProfile -ExecutionPolicy Bypass ^
  -File "%PS_SCRIPT%" "%FILE%" "%PS_SET_FILE%"

if errorlevel 1 (
  echo EDIT_TAGS PowerShell failed
  call :ENSURE_DIR "_Check"
  move "%FILE%" "_Check\" >nul
  endlocal & exit /b 1
)

if not exist "%PS_SET_FILE%" (
  echo EDIT_TAGS: missing PS output
  call :ENSURE_DIR "_Check"
  move "%FILE%" "_Check\" >nul
  endlocal & exit /b 1
)

call "%PS_SET_FILE%"

if defined EDIT_ACTIONS (
  mkvpropedit "%FILE%" --edit info --delete title %EDIT_ACTIONS%
) else (
  mkvpropedit "%FILE%" --edit info --delete title
)
if errorlevel 1 (
    echo mkvpropedit failed
    call :ENSURE_DIR "_Check"
    move "%FILE%" "_Check\" >nul
    endlocal & exit /b 1
)

:EDIT_TAGS_CLEANUP
if exist "%PS_SCRIPT%" del "%PS_SCRIPT%"
if exist "%PS_SET_FILE%" del "%PS_SET_FILE%"

endlocal & exit /b

:RUN_PROBE
setlocal
set "S=" & set "E="
set "PROBE_OK=0"
set "AUTO_CROP="
set "AUTO_RES="
set "PS_SCRIPT=%TEMP%\probe_temp_%RANDOM%.ps1"
set "PS_SET_FILE=%TEMP%\probe_set_vars_%RANDOM%.cmd"
set "PS_STATUS_FILE=%TEMP%\probe_status_output_%RANDOM%.tmp"
if exist "%PS_SET_FILE%" del "%PS_SET_FILE%"
if exist "%PS_STATUS_FILE%" del "%PS_STATUS_FILE%"
for /f "tokens=1 delims=:" %%A in ('findstr /n "^#PS_RUN_PROBE_BEGIN#" "%~f0"') do set /a S=%%A
for /f "tokens=1 delims=:" %%A in ('findstr /n "^#PS_RUN_PROBE_END#"   "%~f0"') do set /a E=%%A-S

if not defined S exit /b 9
set /a E=E
if %E% LEQ 0 exit /b 9

more +%S% "%~f0" | head -n %E% > "%PS_SCRIPT%"

powershell.exe -executionpolicy bypass -file "%PS_SCRIPT%" "%~1" -SetFile "%PS_SET_FILE%" -StatusFile "%PS_STATUS_FILE%"

set "RC=%ERRORLEVEL%"

if exist "%PS_SET_FILE%" call "%PS_SET_FILE%"

%DBG% RUN_PROBE: RC=%RC%
%DBG% RUN_PROBE: NVEnc_Crop=%NVEnc_Crop%
%DBG% RUN_PROBE: NVEnc_Res=%NVEnc_Res%

if exist "%PS_SCRIPT%" del "%PS_SCRIPT%"
if exist "%PS_SET_FILE%" del "%PS_SET_FILE%"
if exist "%PS_STATUS_FILE%" del "%PS_STATUS_FILE%"

if "%RC%"=="0" (
	endlocal & set "AUTO_CROP=%NVEnc_Crop%" & set "AUTO_RES=%NVEnc_Res%" & set "PROBE_OK=1"
	exit /b
)
exit /b

:USAGE
cls
echo Usage: %~n0 ^<encoder(hevc)^> ^<audio(ac3)^> ^<quality(28)^> ^<crop^> ^<filter^> ^<mode^> ^<decoder(avhw)^>
echo.
echo encoder = [def^|hevc^|he10^|h264^|av1]
echo audio   = [copy(1/2/12)^|ac3(lq)^|aac(lq)^|eac3(lq)]
echo quality = [def^|auto^|lq^|ulq^|hq^|uhq]
echo crop    = [copy^|auto^|c(1-6)^|696^|768^|800^|804^|808^|812^|816^|872^|960^|1012^|1024^|1036^|1040^|720(f/p)^|1080(f/p)^|2160(f/p)]
echo           [1440^|1348^|1420^|1480^|1500^|1792^|1764^|1780^|1788^|1800]
echo filter  = [copy^|f(1-6)^|edgelevel^|smooth(31/63)^|nlmeans^|gauss(5)^|sharp^|ss^|denoise(hq)^|artifact(hq)^|superres(hq)]
echo           [vsr^|vsrdenoise(hq)^|vsrartifact(hq)]
echo mode    = [copy^|deint^|yadif(bob)^|double^|23fps^|25fps^|30fps^|60fps^|29fps^|59fps]
echo           [brighter^|darker^|vintage^|linear^|tweak^|HDRtoSDR(R/M/H)^|dv/dolby-vision]
echo decoder = [def^|hw^|sw]
echo.
echo Example: %~n0 ^| encoder ^| audio ^| quality ^| crop ^| filter ^| mode ^| decoder ^|
echo Example: %~n0   hevc      ac3
echo Example: %~n0   hevc      ac3     auto      auto
echo Example: %~n0   hevc      copy    auto      1080   vsr
echo Example: %~n0   hevc      copy    hq        1080   copy
echo Example: %~n0   hevc      copy    def       copy   copy     copy   sw
echo.
goto :END

:NOP
exit /b

:ENSURE_DIR
if not exist "%~1" md "%~1"
exit /b

:DEBUG
setlocal EnableDelayedExpansion
set "DBG_MSG=%*"
echo [DEBUG] !DBG_MSG!
endlocal & exit /b

:END
exit /b 0

#PS_RUN_PROBE_BEGIN#
param(
	[Parameter(Position=0, Mandatory=$true)]
	[string]$VideoFile,
	[Parameter(Mandatory=$true)]
	[string]$SetFile,
	[Parameter(Mandatory=$true)]
	[string]$StatusFile
)
$ExitCode = 0
function Write-Status ($Message) {
	$Timestamp = Get-Date -Format "HH:mm:ss"
	Add-Content -Path $StatusFile -Value "[$Timestamp] $Message"
}
function Get-Median {
	param([int[]]$Numbers)
	$Count = $Numbers.Count
	if ($Count -eq 0) { return 0 }
	$Sorted = $Numbers | Sort-Object
	if ($Count % 2 -eq 1) {
		return $Sorted[[math]::Floor($Count / 2)]
	} else {
		return [int](($Sorted[($Count / 2) - 1] + $Sorted[$Count / 2]) / 2)
	}
}
$StandardResolutions = @{
	384 = @{ Crop="0:192:0:192"; Res="1920x696" }
	312 = @{ Crop="0:156:0:156"; Res="1920x768" }
	280 = @{ Crop="0:140:0:140"; Res="1920x800" }
	276 = @{ Crop="0:138:0:138"; Res="1920x804" }
	272 = @{ Crop="0:136:0:136"; Res="1920x808" }
	268 = @{ Crop="0:134:0:134"; Res="1920x812" }
	264 = @{ Crop="0:132:0:132"; Res="1920x816" }
	208 = @{ Crop="0:104:0:104"; Res="1920x872" }
	120 = @{ Crop="0:60:0:60";	 Res="1920x960" }
	 68 = @{ Crop="0:34:0:34";	 Res="1920x1012" }
	 56 = @{ Crop="0:28:0:28";	 Res="1920x1024" }
	 44 = @{ Crop="0:22:0:22";	 Res="1920x1036" }
	 40 = @{ Crop="0:20:0:20";	 Res="1920x1040" }
	  0 = @{ Crop="0:0:0:0";	 Res="1920x1080" }
}
$StandardWidths = @(1800,1792,1788,1780,1764,1500,1480,1440,1420,1348)
$ffmpegCmd	= "D:\Apps\Commands\bin\ffmpeg.exe"
$ffprobeCmd = "D:\Apps\Commands\bin\ffprobe.exe"
$ProbeTimes = @("00:02:00","00:10:00","00:20:00")
$CropResults = @()
if (-not (Test-Path $VideoFile)) { $ExitCode = 1 }
if ($ExitCode -eq 0) {
	$ResolutionInfo = & $ffprobeCmd -v error -select_streams v:0 -show_entries stream=width,height -of default=noprint_wrappers=1:nokey=1 $VideoFile 2>&1
	$CleanRes = $ResolutionInfo | Where-Object { $_ -match '^\d+$' }
	if ($CleanRes.Count -lt 2) { $ExitCode = 2 }
	$OrigWidth	= [int]$CleanRes[0]
	$OrigHeight = [int]$CleanRes[1]
}
if ($ExitCode -eq 0) {
	foreach ($Time in $ProbeTimes) {
		$out = & $ffmpegCmd -ss $Time -i $VideoFile -t 5 -vf "cropdetect=limit=24:round=2:reset=0" -f null - 2>&1
		$m = [regex]::Matches($out,"crop=(\d+:\d+:\d+:\d+)") | Select-Object -Last 1
		if ($m) {
			$c = ($m.Value -replace 'crop=','') -split ':'
			$CropResults += [PSCustomObject]@{ W=[int]$c[0]; H=[int]$c[1]; X=[int]$c[2]; Y=[int]$c[3] }
		}
	}
	if ($CropResults.Count -eq 0) { $ExitCode = 4 }
}
if ($ExitCode -eq 0) {
	$W = Get-Median $CropResults.W
	$H = Get-Median $CropResults.H
	$X = Get-Median $CropResults.X
	$Y = Get-Median $CropResults.Y
	$CropL=$X; $CropR=$OrigWidth-$X-$W
	$CropT=$Y; $CropB=$OrigHeight-$Y-$H
	$TotalV=$CropT+$CropB
	$TotalV = $CropT + $CropB
	$TotalH = $CropL + $CropR
	if ($TotalV -le 2 -and $TotalH -le 2) {
		"SET NVEnc_Crop=0:0:0:0" | Out-File -Encoding ASCII $SetFile
		"SET NVEnc_Res=${OrigWidth}x${OrigHeight}" | Out-File -Encoding ASCII $SetFile -Append
		exit 0
	}
	if ($TotalV -le 2 -and $TotalH -gt 8) {
		$BestW = $StandardWidths |
			Sort-Object { [math]::Abs($_ - ($OrigWidth - $TotalH)) } |
			Select-Object -First 1
		$Side = [int](($OrigWidth - $BestW) / 2)
		"SET NVEnc_Crop=${Side}:0:${Side}:0" | Out-File -Encoding ASCII $SetFile
		"SET NVEnc_Res=${BestW}x${OrigHeight}" | Out-File -Encoding ASCII $SetFile -Append
		exit 0
	}
	$RejectReason = $null
	if ([math]::Abs($CropT - $CropB) -gt 2) {
		$RejectReason = "vertical asymmetry (T=$CropT B=$CropB)"
	}
	elseif ( ($CropT -eq 0 -and $CropB -gt 0) -or ($CropB -eq 0 -and $CropT -gt 0) ) {
		$RejectReason = "one-sided vertical crop (T=$CropT B=$CropB)"
	}
	elseif ($TotalV -eq 0 -and [math]::Abs($CropL - $CropR) -gt 2) {
		$RejectReason = "horizontal asymmetry (no matching standard width)"
	}
	if ($RejectReason) {
		Write-Status "REJECTED: $RejectReason"
		exit 8
	}
	$Best = $StandardResolutions.GetEnumerator() | Sort-Object { [math]::Abs($_.Key-$TotalV) } | Select-Object -First 1
	$Top=$Best.Value.Crop.Split(':')[1]
	$Bottom=$Best.Value.Crop.Split(':')[3]
	$TargetH=$Best.Value.Res.Split('x')[1]
	$FinalL=0; $TargetW=$OrigWidth
	$NVEncCrop="${FinalL}:${Top}:${FinalL}:${Bottom}"
	$NVEncRes="${TargetW}x$TargetH"
	"SET NVEnc_Crop=$NVEncCrop" | Out-File -Encoding ASCII $SetFile
	"SET NVEnc_Res=$NVEncRes"	| Out-File -Encoding ASCII $SetFile -Append
}
exit $ExitCode
#PS_RUN_PROBE_END#

#PS_EDIT_TAGS_BEGIN#
param(
    [string]$VideoFile,
    [string]$SetFile
)
$ErrorActionPreference='Stop'
$j=& mkvmerge.exe -J "$VideoFile" | ConvertFrom-Json
$actions=@()
function IsPureLang($n){
    if([string]::IsNullOrWhiteSpace($n)){return $false}
    $n -match '^(?i)(Deutsch|Englisch|German|English|French|Stereo|Surround)$'
}
function IsFullWord($n){
    $n -match '^(?i)full$'
}
function NormalizeName($n){
    if([string]::IsNullOrWhiteSpace($n)){return $null}
    if($n -match '(?i)sdh'){ return 'SDH' }
    if($n -match '(?i)forced'){ return 'Forced' }
    return $null
}
$audioGer=@()
foreach($t in $j.tracks){
    if($t.type -eq 'audio' -and $t.properties.language -match '^(?i)(ger|deu)$'){
        $audioGer+=$t
    }
}
$defaultAudioNum=$null
if($audioGer.Count -gt 0){
    $defaultAudioNum=$audioGer[0].properties.number
}
foreach($t in $j.tracks){
    $num=$t.properties.number
    $type=$t.type
    $name=$t.properties.track_name
    if($type -eq 'video'){
        $actions+="--edit track:$num --set language=und --set flag-default=0 --set flag-forced=0 --delete name"
        continue
    }
    if($type -eq 'audio'){
        $actions+="--edit track:$num --set flag-forced=0"
        if($num -eq $defaultAudioNum){
            $actions+="--edit track:$num --set flag-default=1"
        }else{
            $actions+="--edit track:$num --set flag-default=0"
        }
        if(-not [string]::IsNullOrWhiteSpace($name)){
            $normalizedName = NormalizeName $name
            if($null -ne $normalizedName){
                $actions+="--edit track:$num --set name=$normalizedName"
            }elseif(IsPureLang $name){
                $actions+="--edit track:$num --delete name"
            }elseif(IsFullWord $name){
                $actions+="--edit track:$num --delete name"
            }
        }
        continue
    }
    if($type -eq 'subtitles'){
        if($t.properties.forced_track -eq $true){
            $actions+="--edit track:$num --set flag-default=1"
        }
        if(-not [string]::IsNullOrWhiteSpace($name)){
            $normalizedName = NormalizeName $name
            if($null -ne $normalizedName){
                $actions+="--edit track:$num --set name=$normalizedName"
            }elseif(IsPureLang $name){
                $actions+="--edit track:$num --delete name"
            }elseif(IsFullWord $name){
                $actions+="--edit track:$num --delete name"
            }
        }
        continue
    }
}
"SET EDIT_ACTIONS=$($actions -join ' ')" | Out-File -Encoding ASCII -FilePath $SetFile
#PS_EDIT_TAGS_END#
